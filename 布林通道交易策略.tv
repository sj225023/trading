//@version=5

strategy(title="Squeeze + KDJ", overlay=true, initial_capital=1000, default_qty_type=strategy.percent_of_equity, default_qty_value=100)

length = input.int(20, minval=1, title="Length")
src = input.source(close, title="Source")

// 2 Standard Deviation Bollinger Bands
B2mult = input.float(2.0, minval=0.001, maxval=50, title="BB Standard Deviation")
B2basis = ta.sma(src, length)
B2dev = B2mult * ta.stdev(src, length)
B2upper = B2basis + B2dev
B2lower = B2basis - B2dev

p1 = plot(B2upper, color=color.new(#673ab7, 0), linewidth=2, title="Band 2SD upper", style=plot.style_line)
p2 = plot(B2lower, color=color.new(#673ab7, 0), linewidth=2, title="Band 2SD lower", style=plot.style_line)

// 1.5 Keltner channels
useTrueRange = input(true, title="Use True Range")

Kmult = input.float(1.5, title="Keltner Range")
Kma = ta.ema(src, length)
Krange = useTrueRange ? ta.tr : high - low
Krangema = ta.ema(Krange, length)
Kupper = Kma + Krangema * Kmult
Klower = Kma - Krangema * Kmult

plot(Kma, title="Keltner Basis", color=color.new(#2196f3, 0), style=plot.style_line, linewidth=1)

// Highlight Crossover (Squeeze)
bb_condition = input.string(defval="Both", title="Bollinger Band Crossover Condition", options=["Both", "At Least One"])

cross_over_color = if bb_condition == "Both"
    B2upper <= Kupper and B2lower >= Klower ? color.aqua : na
else
    B2upper <= Kupper or B2lower >= Klower ? color.aqua : na




periodK = input.int(25, title="%K Length", minval=1)
smoothK = input.int(2, title="%K Smoothing", minval=1)
k = ta.sma(ta.stoch(close, high, low, periodK), smoothK)


var stopLossPrice = 0.0
lowest = ta.lowest(5)
var entryingL = false
if (cross_over_color == color.aqua and k < 20 and close < B2lower) 
    entryingL := true
 
if (ta.crossover(k,20) and close > open and entryingL)
    entryingL := false
    strategy.entry("做多",strategy.long)
    stopLossPrice := lowest
bgcolor(color=(cross_over_color == color.aqua and k < 20 and close < B2lower) ? color.red : na, transp=20, title="Cross Over Identification Color")
bgcolor(color= strategy.position_size != 0 ? color.green : na, transp=20, title="Cross Over Identification Color")

plot(stopLossPrice,color = color.white)

if strategy.position_size > 0 and ta.crossunder(k,80) or (strategy.position_size > 0 and close < stopLossPrice)
    strategy.close("做多")


